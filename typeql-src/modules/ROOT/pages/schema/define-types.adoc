[#_define_types]
= Define types
:Summary: Define schema types and rules with TypeQL.
:keywords: typeql, schema, model, define, modify, extend, type, rule, annotations
:pageTitle: Define query
:toc: macro
:toclevels: 5
:experimental:
:!table-caption:

A Define query modifies a schema of a database by adding new types or rules,
adding new information to existing type definitions, and replacing existing rules.

Define queries can be used only in a schema session and write transaction.
For more information on the limitations of a schema session, see the
xref:typedb::basics/acid.adoc#_schema_integrity[Schema integrity enforcement].

For more information about basics of data modeling with TypeQL, see the
xref:typeql::fundamentals.adoc[] page.

== Behavior

// tag::behavior[]
A Define query adds new or updates existing types and rules.

Define queries are unambiguous and idempotent
(can be applied multiple times without changing the result of the initial application).
A `define` clause uses a pattern without variables to declare a complete schema definition or
changes to an existing schema.

Define queries are validated with TypeQL syntax.
A database's schema is also validated after applying a given query to validate its consistency.

Define query returns a Promise of an empty response.
// end::behavior[]

=== Types behavior

Every type definition in a Define query is applied to an existing schema of a database.
A Define query should produce a consistent and valid schema in a database,
otherwise it will not pass validation and will be rejected.

== Syntax

A Define query consists of a single `define` clause and always starts with the `define` keyword.

Define queries are written in TypeQL with the following syntax:

[,typeql]
----
define <pattern>
----

A pattern in a `define` clause uses TypeQL to declare type hierarchy, ownerships, roleplayers, and rules.
For rules, see the xref:schema/define-rules.adoc[] page.
For type hierarchy, ownerships, and roleplayers, see below.

[#_define_clause]
== Define clause

A `define` clause is used in a Define query to specify a pattern for new or updated types and rules.
A pattern in a define `clause` consists of schema statements combined by an implicit conjunction (logical `AND`).
These statements can't use any variables or values.
Instead, they use type labels and TypeQL keywords to express
<<_subtyping,subtyping>>,
<<_abstract,abstract flags>>,
<<_ownership,ownership>>,
<<_annotations,annotations>>, and
<<_roleplaying,roleplaying>>.

Statements in a `define` clause pattern can use types that already exist in a schema
(including built-in types), and can use types that are defined anywhere in the same query.
Every statement ends with a semicolon.

[#_schema_patterns]
=== Schema definition patterns

See the table below for the most generic patterns that can be used for schema definition in TypeQL.

.Type patterns
[cols="^.^1,^.^2,^.^2"]
|===
| *Pattern name* | *Pattern syntax* | *Example*

| <<_subtyping,Subtyping>>
| `<label> sub <type>;`
| `person sub user;`

| Direct subtyping
| `<label> sub! <type>;`
| `person sub! user;`

| <<_roleplaying,Adding a role>>
| `<relation> relates <label>;`
| `permission relates access;`

| <<_roleplaying,Assigning a role>> for a type
| `<label> plays <relation>:<role>;`
| `access plays permission:access;`

| <<_ownership,Adding ownership>> to a type
| `<label> owns <attr-type>;`
| `person owns full-name;`
|===

//=== Order of statements and indentation
//#todo Consider generalizing this recommendations

TypeQL statements in a pattern are combined by a conjunction (logical `AND`),
which means they can be included in any order of following with the same result.
For readability purposes, by default, we recommend to include them in the following order:

* Attribute types
* Entity types
* Relation types
* Rules

Indentation and line breaks have no effect on the result of a query and are needed only for readability.

[#_subtyping]
=== Subtyping

Subtyping lets you define a new type, by defining its parent type (or supertype).
You can create subtypes of built-in root types: `attribute`, `entity`, and `relation`.

.Subtyping root types
[,typeql]
----
define

credential sub attribute, value string;
subject sub entity;
access sub relation,
    relates action,
    relates object;
----

Alternatively, you can define a subtype of another user-defined type,
either existing in the current schema or defined in the same query:

.Subtyping user-defined types
[,typeql]
----
define

user sub subject;
person sub user;
----

[#_attributes]
==== Attributes

To define an attribute type, you need to declare its value type.
That is done by adding a comma separated `value` keyword.

.Attributes definition
[,typeql]
----
define

credential sub attribute, value string;
review-date sub attribute, value datetime;
size-kb sub attribute, value long;
validity sub attribute, value boolean;
----

[#_value_types]
The following *value types* are supported in TypeDB:

// tag::value_types[]
* `long` -- a 64-bit signed integer number
* `double` -- a double-precision floating point number, including a decimal point
* `string` -- a string enclosed in double `"` or single `'` quotes
* `boolean` -- a `true` or `false`
* `datetime` -- a date or date-time in one of the following formats:
 ** `yyyy-mm-dd`
 ** `yyyy-mm-ddThh:mm`
 ** `yyyy-mm-ddThh:mm:ss`
 ** `yyyy-mm-ddThh:mm:ss.f`
 ** `yyyy-mm-ddThh:mm:ss.ff`
 ** `yyyy-mm-ddThh:mm:ss.fff`
// end::value_types[]

===== Restrict values with regular expression

A value of an attribute type can be restricted using regular expressions.
For example, to constrain it to a set of options:

.Regular expression value restriction
[,typeql]
----
define

visibility sub attribute, value string, regex "^(public|private|closed)$";
----

The above example defines an attribute type: `visibility`.
It specifies a regex to restrict values to only `public`, `private` and `closed` strings.

[#_relations]
==== Relations

Relation types are defined by subtyping the `relation` root type or other relation types.

Roles are added with the `relates` keyword followed by the role label.
At least one role must be defined for any relation.

Relations can play roles in other relations.

.Relation with two roles
[,typeql]
----
define

access sub relation,
    relates object,
    relates action;
----

For any instance to be able to play a role in a relation,
the type of the instance must have that role defined as playable by it with the `plays` keyword.
For more information on letting a type to play a role in a relation type, see the <<_roleplaying>> section.

[#_abstract]
=== Abstract

Optionally, types can be defined as `abstract`.
xref:typeql::fundamentals.adoc#_abstract_types[Abstract types] can't be instantiated but can be queried,
as their subtypes can be concrete types.

.Define abstract type example
[,typeql]
----
define

subject sub entity, abstract;
----

You can declare an existing schema type as abstract to make it abstract if there is no data of this type in a database.
You can declare an abstract type non-abstract by sending the same type definition without the `abstract` keyword.

[#_ownership]
=== Ownership

To add ability for a type to own instances of an attribute type,
use the `owns` keyword followed by the label of the attribute type.

Ownership definitions can be defined as separate statements
or combined with general owner type definition with a comma separator:

[,typeql]
----
define

full-name sub attribute, value string;
object-type sub attribute, value string;

person sub user;
person owns full-name;

object sub entity, owns object-type;
----

The above definition adds an ability for instances of `person` type to own instances of the `full-name` type.

[#_cardinality]
=== Cardinality

By default, an instance of a type can have ownership of multiple attributes of the same attribute type
with different values.

By having multiple attributes of the same type we're effectively creating a
xref:typeql::data/insert.adoc#_multivalued_attributes[multivalued attribute]
(as if having an attribute with multiple values).

Use the <<_key_attribute,Key attribute annotation>>
to limit the cardinality to exactly *one* and add *uniqueness* constraint.
Hence, the instance of the type with key annotated ownership will have exactly one (no more and no less)
key attribute instance.

See the example in the <<_key_attribute,Key attribute>> section.

More flexible cardinality annotation is expected to be available with the release of TypeDB v.3.0.
//NoteL 3.0 feature

[#_annotations]
=== Annotations

Annotations are added for an ownership of an attribute type in a schema
to add constraints on what attributes can be owned by an instance of an owner type.

[#_unique_attribute]
==== Unique attribute annotation

Use the `@unique` keyword to add *uniqueness* constraint to the ownership of an attribute.
This prevents two instances of the same type from owning the same attribute instance (with the same value).

[,typeql]
----
define

object sub entity, owns object-type @unique;
----

Unique annotation can be inherited, even using override of an ownership:

.See example
[,typeql]
----
define

organisation sub entity,
    abstract,
    owns organisation-id @unique;

organisation-id sub attribute, abstract, value string;

non-profit sub organisation,
    owns nonprofit-id as organisation-id;

nonprofit-id sub organisation-id, value string;
----

In this example `non-profit` owns `nonprofit-id` with unique constraint.
It's inherited from `organizatrion-id`.

[#_key_attribute]
==== Key attribute annotation

Use the `@key` keyword to set the owned attribute as key.
That limits the cardinality to exactly *one* and adds *uniqueness* constraint.
Hence, the instance of the type with attribute ownership with `@key` annotation will have exactly one
(no more and no less) key attribute instance, and it will be unique for all instances of the same type.

[,typeql]
----
define

object sub entity, owns object-type @key;
----

[NOTE]
====
An ownership can't have both `@unique` and `@key` annotations at the same time.
====

[#_roleplaying]
=== Roleplaying

For any instance to be able to play a role in a relation,
the type of the instance must be defined as a potential roleplayer:

.Roleplayer type
[,typeql]
----
define

access sub relation, relates object;
object sub entity, plays access:object;
----

The above examples define the `object` type being able to play a role `object` in the `access` relation type.
With this definition in a schema, we can now xref:typeql::data/insert.adoc[insert]
data in our database about a relation of type `access` with the role `object` played by a given instance
of an entity type `object`.

=== Inheritance

A subtype inherits parent's owned attributes and roles.

.Inheritance
[,typeql]
----
define

path sub attribute, value string;

object sub entity,
    abstract,
    owns object-type,
    plays access:object;
resource sub object, abstract;
file sub resource, owns path;
----

In the above example, the `resource` and `file` entity types are subtypes of the `object` type,
which itself is a subtype of the `entity` root type.
They inherit the `object-type` attribute type ownership as well as `access:object` role.
However, while the `resource` subtype is abstract, the `file` subtype is not.
Hence, we can create entities of `file` type, but not `resource` entities.

Further, the `path` attribute type will be owned by the `file` entity type directly.

==== Override inherited ownership

To override an inherited ownership, use the `owns` keyword with the new owned attribute type,
followed by the `as` keyword and the inherited attribute type:

.Override inheritance
[,typeql]
----
define file sub resource, owns file-type as object-type;
----

The new attribute type that overrides inherited type is defined in the schema as subtype of the inherited
attribute type.
Hence, the inherited attribute type is an xref:typeql::fundamentals.adoc#_abstract_types[abstract type]
and has the same value type as the new attribute type.
The example above in a its complete form in a schema would look like this:

.Override inheritance with attributes definitions
[,typeql]
----
define

path sub attribute, value string;

object-type sub attribute, abstract, value string;
file-type sub object-type, value string;

object sub entity,
    abstract,
    owns object-type;
resource sub object,
    abstract;
file sub resource,
    owns path,
    owns file-type as object-type;
----

In the above example, the `file` entity type owns the `path` and `file-type` attribute types.
The `file-type` attribute type overriding the inherited `object-type` attribute type.

[#_overrides_a_role]
==== Override a role

The label of an inherited role can be overridden to distinguish between roles inherited by a relation subtype
versus roles defined by its parent type.

.Overriding roles
[,typeql]
----
define

membership sub relation,
    relates parent,
    relates member;

collection-membership sub membership,
    relates collection as parent;
----

In the above example, the `collection-membership` relation type subtypes the `membership` relation type and overrides
the inherited `parent` role as `collection`.
The inherited `member` role is inherited as it is.

By overriding an inherited role, we prevent subtype from relating the role that would otherwise be inherited.

== Examples

=== Simple type hierarchy

Simple type hierarchy schema example:

[,typeql]
----
include::home::attachment$quickstart-schema.tql[]
----

The above examples defines three attribute types and two entity types.
The `person` entity types is a subtype of the `user` and inherits its ownership of some attributes.
Unlike `user` the `person` type is concrete (not abstract) and can be instantiated (inserted to a database as data).

=== IAM schema

For more complete and sophisticated example of a schema, see the following default IAM schema used in our documentation:

[,typeql]
----
include::typedb::attachment$iam-schema.tql[lines=17..]
----

For an explanation of this schema, see the xref:typedb::tutorials/iam-schema.adoc[] page.
