= Advanced patterns & queries
:Summary: Targeting instances of data or schema types that match a TypeQL pattern.
:keywords: typeql, query, match, pattern, statement, variable
:longTailKeywords: typeql match, query pattern, match clause, typeql variables, answers
:pageTitle: Advanced patterns & queries
:toc: macro
:toclevels: 5

Explore the xref:typeql::data/basic-patterns.adoc[] page before continue reading this page.

.See the full table of contents for this page
[%collapsible]
====
toc::[]
====

[#_pattern_syntax]
== Pattern syntax

=== Schema

The statements below can be used as standalone patterns or
xref:typeql::data/basic-patterns.adoc#_combining_statements[combined] with other statements
to find types defined in a database's schema.
Use the xref:schema/define-types.adoc[Define] query to try them.

[cols="^.^1,^.^2,^.^2"]
|===
| *What we are looking for in a schema* | *Pattern syntax* | *Example*

| Type
| `$<var_label> type <type>;`
| `$t type person;`

| Type or subtype
| `$<var_label> sub <type>;`
| `$t sub subject;`

| Direct subtype
| `$<var_label> sub! <type>;`
| `$t sub! subject;`

| Role in a relation type
| `<relation> relates $<var_label>;`
| `permission relates $r;`

| Role player's type in a relation
| `$<var_label> plays <relation>:<role>;`
| `$r plays permission:subject;`

| Owner type of an attribute type
| `$<var_label> owns <attr-type>;`
| `$o owns full-name;`

| Attribute subtype
| `$<var> sub[!] <attribute-type> [, value <value-type>];`
| `$s sub id, value string;`
|===

=== Data

The statements below can be used as a standalone patterns or in a
xref:typeql::data/basic-patterns.adoc#_combining_statements[combination] with other statements
to find data in a database.
Use the xref:data/get.adoc[Get] query to try them.

[cols="^.^1,^.^2,^.^2"]
|===
| *What we are looking for in data* | *Pattern syntax* | *Examples*

| Instance of a type or a subtype
| `+$<var_label> isa <type> [, <has-attribute statement>...];+`
| `$p isa person, has $a;`

| Instance of a type
| `+$<var_label> isa! <type> [, <has-attribute statement>...];+`
| `$p isa! person, has $a;`

| Instance of an attribute type
| `+$<var_label> [isa[!] <attr-type>] (contains "<text>" \| like "<regex>" \| <value>) [, <has-attribute statement>...];+`
a|
* `$a "Kevin Morrison";`
* `$b isa full-name;`
* `$c contains "Kevin";`

| Instance of a relation
| `+[$<var_label>] ([<role>:] $<var_label> [, [<role>:] $<var_label>]...) isa <relation-type> [, <has-attribute statement>...];+`
| `$pe (subject: $p, access: $ac) isa permission, has validity true;`

| has-attribute statement
| `+has <attr-type> ($<var_label> \| [<comparison-operator>] <value> );+`
| `$f has size-kb > 300;`

| comparison
| `$<var_label> [<comparison-operator>] <value>;`
| `$a > 300; $f has size-kb $a;`

| equal
| `$<var_label> is $<var_label>;`
| `$p isa person, has $a; $t isa entity; $t is $p; get $t;`
|===

For more information on xref:typeql::data/basic-patterns.adoc#_comparison_operators[comparison operators],
xref:typeql::data/basic-patterns.adoc#_computation[computation], and
xref:typeql::data/basic-patterns.adoc#_combining_statements[combining statements]
-- see the xref:typeql::data/basic-patterns.adoc[] page.

== Matching patterns examples

[NOTE]
====
The patterns below are designed for the IAM
xref:typedb::attachment$iam-schema.tql[schema] and
xref:typedb::attachment$iam-data.tql[dataset], but they can return any number of results:

* zero,
* one,
* multiple.
====

=== Schema

* <<_type_matching>>
* <<_role_matching>>

[#_type_matching]
==== Type matching

* <<_all_types>>
* <<_specific_type_or_nested_subtype>>
* <<_direct_subtypes>>
* <<_attribute_types_by_value_type>>
* <<_specific_type>>
* <<_players_of_a_specific_role>>
* <<_owners_spec_type>>

[#_all_types]
===== All types

Use a `subtype` pattern with the built-in type `thing` to find all types defined in a schema. For more information, see
the xref:typeql::fundamentals.adoc#_thing_type[Thing type] section.

[,typeql]
----
match $t sub thing;
----

include::typedb::partial$thing-warning.adoc[]

[#_specific_type_or_nested_subtype]
===== Specific type or nested subtype

Use a `subtype` pattern to find a specific type and all of its subtypes.

[,typeql]
----
match $o sub object;
----

The above pattern finds the `object` type and all of its nested subtypes: direct (i.e., `resource` and `resource-collection`)
and indirect (i.e., `file`, `interface`, `directory`, and `application`).

[#_direct_subtypes]
===== Direct subtypes

Use a `subtype` pattern with an exclamation mark (`!`) to find the direct subtypes of a specific type.

[,typeql]
----
match $o sub! object;
----

The above query finds all direct subtypes of the `object` type (i.e., `resource` and `resource-collection`).

[#_attribute_types_by_value_type]
===== Attribute types (by value type)

Use an `attribute subtype` pattern to find all attribute types with a specific value type.

[,typeql]
----
match $a sub attribute, value boolean;
----

The above query finds all attribute types that have a `boolean` value type.

[#_specific_type]
===== Specific type

Use a `type` pattern to find a specific type, excluding any nested subtypes (direct or indirect).

[,typeql]
----
match $o type object;
----

The above query returns the `object` type, and none of its nested subtypes (direct or indirect).

[#_players_of_a_specific_role]
===== Players of a specific role

Use a "players type in a relation" pattern to find all types that play a specific role in a specific relation type.

[,typeql]
----
match $p plays permission:subject;
----

The above query finds all types that can play the `subject` role in the `permission` relation type.

[#_owners_spec_type]
===== Owners of a specific attribute type

Use an `owners of attribute type` pattern to find all types that own a specific attribute type.

[,typeql]
----
match $o owns full-name;
----

The above query finds all types that own the `full-name` attribute.

[#_role_matching]
==== Role matching

Use the `role types in a relation` pattern to find all roles in a specific relation.

[,typeql]
----
match permission relates $r;
----

The above query finds all the roles defined in the `permission` relation type (`permission:access` and
`permission:subject`).

=== Data

* <<_all_data>>
* <<_entity_matching>>
* <<_relation_matching>>
* <<_attribute_matching>>
* <<_equality>>
* <<_computation>>
* <<_conjunctions>>
* <<_disjunctions>>
* <<_complex_example>>

[#_all_data]
==== All data

Use an `instance` pattern with type `thing` to find all entities, relations, and attributes (instances of data).

[,typeql]
----
match $t isa thing;
----

For more information on `thing` type see the xref:typeql::fundamentals.adoc#_thing_type[Thing type] section.

include::typedb::partial$thing-warning.adoc[]

[#_entity_matching]
==== Entity matching

===== Instances of the specific type or its subtypes

Use an `instance` pattern to find all entities of a specific entity type (and of all its subtypes).

[,typeql]
----
match $p isa person;
----

The above query returns all entities of the `person` entity and any of its subtypes.

[IMPORTANT]
====
The `isa` keyword will return all direct and indirect (of all subtypes of the given type) instances.
To limit results to only direct instances of the given type use `isa!` instead. See the
<<_specific_types_instances,example>> below.
====

[#_specific_types_instances]
===== Instances of the specific type only

Use an `instance` pattern with an exclamation mark (`!`) to find all entities of a specific type only
(and none of its subtypes).

[,typeql]
----
match $u isa! user;
----

The above query finds all `user` entities. It excludes any entities whose type is a subtype of `user`,
so `person` entities would not be included.

===== Owners of a specific attribute type

Use an `instance` pattern with `has-attribute statement` pattern to find all entities that own an attribute of a
specific type.

[,typeql]
----
match $p isa person, has full-name $n;
----

The above query finds all `person` entities that own a `full-name` attribute.

===== Owners of specific attribute types (multiple)

Use an `instance` pattern with multiple `has-attribute statements` to find all entities that own a specific set of
attributes, each of a specific type.

[,typeql]
----
match $p isa person, has full-name $n, has email $email, has credential $cr;
----

The above query finds all `person` entities that have `full-name`, `email`, and `credential` attributes.

===== Owners of a specific attribute (by type and value)

Use an `instance` pattern with `has-attribute statement` pattern to find all entities that own a specific attribute
with a specific value.

[,typeql]
----
match $p isa person, has full-name “Kevin Morrison”;
----

The above query finds all `person` entities that have a `full-name` attribute with a value of "`Kevin Morrison`".

===== Owners of a specific attribute (by type and value range)

Use an `instance` pattern with a `has-attribute statement` containing a comparison operator to find all entities that
have a specific attribute whose value is within a specific range.

[,typeql]
----
match $f isa file, has size-kb < 100;
----

However, if the attribute value itself is required in the query response, combine a `has-attribute statement`
(with no comparison operation) with a separate `comparison` pattern.

[,typeql]
----
match
  $f isa file, has size-kb $s;
  $s < 100;
----

[#_relation_matching]
==== Relation matching

The pattern used to find relations is different from the one used to find entities because relations have role players
(entities, other relations, and/or attributes).

===== Role players

Use a `relation pattern` with a matched role player to find all relations of a specific type that relate a
specified instance.

[,typeql]
----
match
  $p isa person, has full-name "Kevin Morrison";
  $pe (subject: $p) isa permission;
----

The above query finds all `person` entities (`$p`) owning a `full-name` attribute with a value of `Kevin Morrison`
and then it finds all `permission` relations (`$pe`) in which those entities `$p` play the `subject` role.

===== Owners of a specific attribute (with value)

Use a `relation pattern` with a `has-attribute statement` to find all relations of a specific type and which have a
specific attribute with a specific value.

[,typeql]
----
match $pe (subject: $p, access: $ac) isa permission, has validity true;
----

The above query finds all `permission` relations which have a `validity` attribute whose value is `true`.

===== With no relation variable

The relation variable can be omitted when only the role players are needed.

[,typeql]
----
match (subject: $p, access: $ac) isa permission;
----

===== With no role names

The names of a relation's roles can be omitted.

[,typeql]
----
match $pe ($p, $ac) isa permission;
----

This will match any valid combination of roles.

[#_attribute_matching]
==== Attribute matching

There are multiple ways to find attributes depending on the use case.

===== Value

Use a variable and an attribute value to find all attributes with a specific value.

[,typeql]
----
match $x "Masako Holley";
----

The above query finds all attributes with a value of "`Masako Holley`", regardless of their type.

===== Type and value

Combine an `attribute` pattern with a `comparison` pattern to find all attributes of a specific type and with a
specific value.

[,typeql]
----
match
  $n isa full-name;
  $n "Masako Holley";
----

Or use this compact form:

[,typeql]
----
match $n "Masako Holley" isa full-name;
----

The above queries finds all `full-name` attributes with a value of `Masako Holley`.

===== Value containing

Use an `attribute` pattern with `contains` keyword to find all attributes whose value contains specific string.

[,typeql]
----
match $name contains "Masako";
----

The above query finds all attributes whose value contains the text `Masako`, regardless of their type.

===== Value matching regex

Use an `attribute` pattern with `like` keyword and a regular expression to find all attributes whose value matches
the specified regular expression pattern.

[,typeql]
----
match $x like "(Masako Holley|Kevin Morrison)";
----

The above query finds all attributes whose value is `Masako Holley` or `Kevin Morrison`, regardless of their type.

[#_equality]
==== Equality

Use an `equal` pattern to check if two variables represent the same instance (or instances).
That not only means equality by type and value but literary being the same instance(s) of data in a database.
It is often helpful in negation.

[,typeql]
----
match
  $x isa person;
  $y isa person;
  not { $x is $y; };
----

The above query returns pairs of users that are not the same user.

[#_computation]
==== Computation

[#_using_parentheses]
===== Using parentheses

[,typeql]
----
match
  $f isa file, has size-kb $s;
  ?x = ($s + 5) * 2;
----

In the above example parentheses are used to change order of operations: addition inside the parentheses will happen
before multiplication.

[#_using_exponentiation]
===== Using exponentiation

[,typeql]
----
match
  $f isa file, has size-kb $s;
  ?x = $s ^ 2;
----

In the above example we set value variable `?x` to be equal to the result of exponentiation:
value of `$s` raised to the power of 2.

[#_using_multiplication]
===== Using multiplication

[,typeql]
----
match
  $f isa file, has size-kb $s;
  ?x = $s * 3;
----

In the above example we multiply the value of `$s` by 3 and set the value variable `?x` to be equal to the result.

[#_using_division]
===== Using division

[,typeql]
----
match
  $f isa file, has size-kb $s;
  ?x = $s / 3;
----

In the above example we divide the value of `$s` by 3 and set the value variable `?x` to be equal to the result.

[#_using_modulo]
===== Using modulo

[,typeql]
----
match
  $f isa file, has size-kb $s;
  ?x = $s % 3;
----

In the above example we set the value variable `?x` to be equal to the result of modulo operation between `$s` and 3.
Or `$s` mod 3.

[#_using_addition]
===== Using addition

[,typeql]
----
match
  $f isa file, has size-kb $s;
  ?x = $s + 1250;
----

In the above example we add the value of `$s` to 1250 and set the value variable `?x` to be equal to the result.

[#_using_subtraction]
===== Using subtraction

[,typeql]
----
match
  $f isa file, has size-kb $s;
  ?x = $s - 300;
----

In the above example we subtract 300 from the value of `$s` and set the value variable `?x` to be equal to the result.

[#_using_minimum_function]
===== Using minimum function

[,typeql]
----
match
  $f isa file, has size-kb $s;
  ?x = $s - 300;
  ?a = min($s, ?x);
----

In the above example we set the value variable `?x` to be equal to either `$s` or `?x`, whichever is the minimum.

[#_using_maximum_function]
===== Using maximum function

[,typeql]
----
match
  $f isa file, has size-kb $s;
  ?x = $s - 300;
  ?a = max($s, ?x);
----

In the above example we set the value variable `?x` to be equal to either `$s` or `?x`, whichever is the maximum.

[#_using_floor_function]
===== Using floor function

[,typeql]
----
match
  $f isa file, has size-kb $s;
  ?x = floor($s / 3);
----

In the above example we set the value variable `?x` to be equal to the result of floor function (the greatest integer
less than or equal to the argument).

[#_using_ceiling_function]
===== Using ceiling function

[,typeql]
----
match
  $f isa file, has size-kb $s;
  ?x = ceil($s / 3);
----

In the above example we set the value variable `?x` to be equal to the result of ceiling function (the least integer
greater than or equal to the argument).

[#_using_rounding_function]
===== Using rounding function

[,typeql]
----
match
  $f isa file, has size-kb $s;
  ?x = round($s / 3);
----

In the above example we set the value variable `?x` to be equal to the result of rounding function (rounding to the
nearest integer, half up).

[#_using_absolute_value_function]
===== Using absolute value function

[,typeql]
----
match
  $f isa file, has size-kb $s;
  ?x = abs($s - 1000);
----

In the above example we set the value variable `?x` to be equal to the result of absolute value function (discarding
the sign).

[#_conjunctions]
==== Conjunctions

By default, a collection of statements in a `match` clause, divided by semicolons, constructs a conjunction of
statements.

[,typeql]
----
match
  $p isa person, has full-name "Kevin Morrison";
  $o isa object, has path $o-path;
  $ac(object: $o) isa access;
  $pe(subject: $p, access: $ac) isa permission;
----

The above example uses conjunction to ensure all statements are matched:

. Find all `person` entities (`$p`) that have a `full-name` attribute whose value is `Kevin Morrison`.
. Find all `object` entities (`$o`) that have a `path attribute` (`$o-path`).
. Find all `access` relations (`$ac`) where `$o` plays the `object` role.
. Find all `permission` relations (`$pe`) where `$p` plays the `subject` role, `$ac` the `access` role.

The `object` entities are limited to those which play the `object` role in `access` relations, which are themselves
limited to those which play the `access` role in `permission` relations. The `permission` relations are limited to
those in which matching `person` entities play the `subject` role.

[#_disjunctions]
==== Disjunctions

To include statements in the form of a disjunction, we need to wrap each statement in `{}` and place the `or` keyword
in between them.

[,typeql]
----
match
  $p isa person, has full-name $n;
  { $n contains "Masako"; } or { $n contains "Kevin"; };
get $p;
----

The above query uses disjunctions to ensure one of two statements are matched:

. Finds all `person` entities that have a `full-name` attribute (`$n`).
. Checks to see if `$n` contains the text "`Masako`" *OR* if `$n` contains the text "`Kevin`":
 ** If either statement is true, there is a match.
 ** If neither statement is true, there is NO match.

The `person` entities are limited to those that have a `full-name` attribute whose value contains `Masako` or `Kevin`.

[#_complex_example]
==== Complex example

To better illustrate the possibilities, we will now look at an example of a more complex pattern.

image::example-pattern.png[Complex example]

The pattern is a conjunction of five different pattern types:

. *Conjunction 1* specifies the variables for two `person` instances, their `full-names`, `action`, and `file` that
has path `README.md`, specifies their types.
. *Disjunction* specifies that the actions of interest are either `modify_file` or `view_file`.
. *Negation 1* specifies that person `$p1` shall not have `full-name` with value of `Masako Holley`.
. *Negation 2* specifies that person `$p2` shall not have `full-name` with value of `Masako Holley`.
. *Conjunction 2* defines the pattern requiring the `file` to have `access` with `action` that we specified earlier,
and both instances of `person` to have a `permission` to the specified `access`.

In short, the above example finds pairs of people who both have permission to access the same file
with a path of `README.md`. The pattern additionally specifies both of them to not have name `Masako Holley` and
the access to be either `modify_file` or `view_file`.

== Query examples

Use the examples below to try different types of queries to a database's data (querying for data instances).

=== Get query

[,typeql]
----
match
  $p isa person, has full-name $f, has email $e;
get $p, $e;
group $p;
count;
----

The above example retrieves all instances of the `person` type, that have ownership over at least one instances of
`full-name` attribute type and at least one instance of `email` attribute.
We then filter the response to contain only the `$p` and the `$e` in every result.
We further add modifiers to group results by the `$p` variable and count the number of results in every group.

The resulted response should look like this:

[,typeql]
----
iid 0x826e800f8000000000000000 isa person => 1
iid 0x826e800f8000000000000001 isa person => 1
iid 0x826e800f8000000000000002 isa person => 1
----

In the example above we got an answer of the number of emails for every person with an email and full-name in the
database.

Without modifiers the response would look like the following:

[,typeql]
----
{
    $p iid 0x826e800f8000000000000001 isa person;
    $e "pearle.goodman@vaticle.com" isa email;
}
{
    $p iid 0x826e800f8000000000000002 isa person;
    $e "kevin.morrison@vaticle.com" isa email;
}
{
    $p iid 0x826e800f8000000000000000 isa person;
    $e "m.holley@vaticle.com" isa email;
}
----

[WARNING]
====
The `iid` field is an internal id, used to address in query results different instances that doesn't have a value.

It should not be used for any logic as there are no guarantees provided regarding it.
It may be deprecated in future versions of TypeDB.
====

For more information on how to use the Get query please see the xref:typeql::data/get.adoc[] page.

=== Insert query

To write new data to a database use the insert query.

[,typeql]
----
insert
  $p isa person, has email "bob@vaticle.com", has full-name "Bob";
----

The above query inserts compeltely new data of an instance of the `person` entity type that has `email` attribute with
the value `bob@vaticle.com` and `full-name` attribute with the value `Bob`.

To add data to the existing one we can use the optional `match` clause in the insert query:

[,typeql]
----
match
  $p isa person, has full-name "Bob";
insert
  $p has email "bob2@vaticle.com";
----

The above example matches all instances of the `person` type and all its subtypes that have a `full-name` attribute
with the value of `Bob`.

Then it inserts the additional data that all matched instances of `person` have ownership over the `email`
attribute type instance with the value `bob2@vaticle.com`.

For more information on the Insert query please see the xref:typeql::data/insert.adoc[] page.

=== Delete query

To delete data from a database use the delete query.

[,typeql]
----
match
  $p isa person, has full-name "Bob", has email $e;
  $e "bob2@vaticle.com";
delete
  $p has email $e;
----

The above example matches all instances of the `person` type and all its subtypes that have ownership over at least
one instance of `full-name` attribute type with the value `Bob` and at least one instance of the `email` type with
the value equal to the `$e` variable. The `$e` variable is set to have a value of `bob2@vaticle.com`.

For all matched results the query deletes the exact ownership of the `email` instance with the value equal to the `$e`
by the `$p`. The `person` instance in `$p` and the `email` instance in `$e` continue to exist in the database. Only the
ownership between them gets deleted.

For more information on how to delete data please see the xref:typeql::data/delete.adoc[] page.

=== Update

To update data in a database we use a combined `match-delete-insert` query.

[,typeql]
----
match
  $p isa person, has full-name "Bob", has email $email;
delete
  $p has $email;
insert
  $p has email "deleted";
----

The above example matches all instances of the `person` type and all its subtypes that have a `full-name` attribute
with the value `Bob` and have ownership over at least one instance of the `email` attribute type.

It proceeds with deleting the ownership of any matched `email` attribute instances that the matched `person` instances
have.

Finally, it inserts the data that all matched instances of `person` type have ownership over the `email` attribute with
the value `deleted`.

[NOTE]
====
The `$email` variable can only have one concept for every matched pattern (solution for the `match` clause). So if
there will be more than one e-mail matched, then the result of match clause will contain multiple solutions.

Both delete clause and insert clause will be executed once for every matched pattern. But the insertion of ownership of
the instance of the `email` attribute type with the same value twice will not produce any duplication. Because any
attribute instance can be owned only once by the same owner. Hence, all previously existent "email" attributes will
be deleted and only one inserted.
====

For more information on how to use the Update query see the xref:typeql::data/update.adoc[] page.

=== Complex pattern query example

A `match` clause can only address data or types that already exist in a database.

The *declarative* nature of the TypeQL pattern means that if one of the `match` clause statements can't find any
instances in a database that might lead to finding no matches/solutions/answers to a query with that pattern.

For example:

[,typeql]
----
match
  $p isa person, has full-name $p-fname;
  $o isa object, has path $o-path;
  $a isa action, has name "view_file";
  $ac(object: $o, action: $a) isa access;
  $pe(subject: $p, access: $ac) isa permission;
  $p-fname = "Kevin Morrison";
----

The above query does the following:

. Finds all `person` entities (`$p`) that have `full-name` attribute with the value of `$p-fname` variable, that is
later set as string `Kevin Morrison`. There is actually only one such person in the *IAM database* by default.
. Finds all `object` entities (`$o`) that have `path` attribute (`$o-path`). The value of the attribute is not
limited but assigned a variable `$o-path`.
. Finds all `action` entities (`$a`) that have `name` attribute with the value of `view_file`. There is
actually only one such action in the *IAM database* by default.
. Finds all `access` relations (`$ac`) that relate `$o` (as `object` role) to `$a` (as `action` role).
. Finds all `permission` relations (`$pe`) that relate `$p` (as `subject` role) to `$ac` (as `access` role).
. States that `$p-fname` variable equals by value to a string `Kevin Morrison`.

This `match` clause can be a part of any query. For example, by appending a `delete $pe isa permission` statement
we can create a delete query that deletes all access to action `view_file` on all objects with any path attribute
owned for a person `Kevin Morrison`.

If we change the value of the `$p-fname` variable to something nonexistent in our database then the
`match` clause will return no results. Appending the same `delete` statement to such `match` clause would create a
useless query that can't delete anything because it can't match any data to delete.
