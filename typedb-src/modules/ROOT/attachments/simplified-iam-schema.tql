#
# Copyright (C) 2022 Vaticle
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public License as
# published by the Free Software Foundation, either version 3 of the
# License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Affero General Public License for more details.
#
# You should have received a copy of the GNU Affero General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.
#
define

credential sub attribute, value string;
full-name sub attribute, value string;
id sub attribute, abstract, value string;
email sub id, value string;
name sub id, value string;
path sub id, value string;
size-kb sub attribute, value long;

subject sub entity,
    abstract,
    owns credential,
    plays permission:subject;

user sub subject,
    owns email @key,
    abstract;

person sub user,
    owns full-name;

object sub entity,
    abstract,
    plays access:object;

file sub object,
    owns path,
    owns size-kb;

action sub entity,
    owns name,
    plays access:action;

operation sub action;

access sub relation,
    relates action,
    relates object,
    plays permission:access;

permission sub relation,
    relates access,
    relates subject;

rule add-view-permission: when {
    $modify isa action, has name "modify_file";
    $view isa action, has name "view_file";
    $ac_modify (object: $obj, action: $modify) isa access;
    $ac_view (object: $obj, action: $view) isa access;
    (subject: $subj, access: $ac_modify) isa permission;
} then {
    (subject: $subj, access: $ac_view) isa permission;
};
